if (!isLibrary.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

android {
    namespace "com.moko.mkgw4"
    compileSdk 35

    defaultConfig {
        minSdk 28
        targetSdk 35
        versionCode 111
        versionName "1.1.1"

        def formatDate = new Date().format("yyyyMMddHHmm")
        setProperty("archivesBaseName", "MKGW4_${versionName}_${formatDate}")
    }
    android.buildFeatures.buildConfig true

    buildTypes {
        release {
            if (!isLibrary.toBoolean()) {
                buildConfigField("boolean", "IS_LIBRARY", "false")
            } else {
                buildConfigField("boolean", "IS_LIBRARY", "true")
            }
            minifyEnabled false
        }
        debug {
            if (!isLibrary.toBoolean()) {
                buildConfigField("boolean", "IS_LIBRARY", "false")
            } else {
                buildConfigField("boolean", "IS_LIBRARY", "true")
            }
            minifyEnabled false
        }
    }

//    compileOptions {
//        sourceCompatibility JavaVersion.VERSION_1_8
//        targetCompatibility JavaVersion.VERSION_1_8
//    }

    viewBinding {
        enabled = true
    }

    lintOptions {
        disable 'SetTextI18n'
        checkReleaseBuilds false
    }

    sourceSets {
        main {
            if (!isLibrary.toBoolean()) {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            }
        }
    }

    lintOptions {
        abortOnError false
    }

    if (isLibrary.toBoolean()) {
        libraryVariants.all { variant ->
            if (variant.buildType.name == "debug") {
                // 修改aar名称
                variant.outputs.all { output ->
                    def formatDate = new Date().format("yyyyMMddHHmm")
                    outputFileName = "MKGW4_${android.defaultConfig.versionName}_${formatDate}.aar"
                }
                // 复制aar到指定目录
                variant.assemble.doLast {
                    variant.outputs.all { output ->
                        // 输出目录在：项目目录/build/outputs/aar/
                        def outputPath = [project.rootDir.absolutePath, "build", "outputs", "aar"].join(File.separator)
                        copy {
                            from output.outputFile
                            into outputPath
                        }
                    }
                }
            }
        }
    }
    packagingOptions {
        exclude 'META-INF/kotlinx_coroutines_core.version'
    }
}

dependencies {
    implementation(platform("org.jetbrains.kotlin:kotlin-bom:1.8.0"))
    if (!isLibrary.toBoolean()) {
        implementation project(path: ':mokosupport')
        implementation files('libs/jxl.jar')
        implementation 'com.github.MOKO-Android-Base-Library:MKScannerIoTLib:1.0.8'
    } else {
        implementation files('libs/mokoSupport.jar')
        compileOnly 'com.github.MOKO-Android-Base-Library:MKScannerIoTLib:1.0.8'
        compileOnly 'com.github.MOKO-Android-Base-Library:MKBleLib:1.0.1'
        compileOnly files('libs/jxl.jar')
    }
}
